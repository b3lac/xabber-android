apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'
apply plugin: 'com.frogermcs.androiddevmetrics'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 393
        versionName '2.1(393)'
    }

    lintOptions {
        // problems with generated "applicationId" string resource
        disable 'MissingTranslation'
        disable 'InvalidPackage'

        abortOnError false
    }

    subprojects {
        afterEvaluate {
            if (getPlugins().hasPlugin('android') ||
                    getPlugins().hasPlugin('android-library')) {

                configure(android.lintOptions) {
                    abortOnError false

                }
            }
        }
    }

    productFlavors {

//        dev {
//            applicationId "com.xabber.androiddev"
//            resValue 'string', 'application_package', applicationId
//            multiDexEnabled true
//        }

        beta {
            applicationId "com.xabber.android.beta"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        prod {
            applicationId "com.xabber.android"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        vip {
            applicationId "com.xabber.androidvip"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            // enable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true
        }
        debug {
            // disable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips', 'x86', 'x86_64'

            universalApk true
        }
    }

    afterEvaluate {
        initFabricPropertiesIfNeeded()
    }
}

def initFabricPropertiesIfNeeded() {
    def propertiesFile = file('fabric.properties')

    def apiSecret = hasProperty('crashlyticsApisecret') ? crashlyticsApisecret : System.getenv('crashlyticsApisecret')
    def apiKey = hasProperty('crashlyticsApikey') ? crashlyticsApikey : System.getenv('crashlyticsApikey')

    if (!propertiesFile.exists()) {
        def commentMessage = "This is autogenerated fabric property from system environment to prevent key to be committed to source control."
        ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
            entry(key: "apiSecret", value: apiSecret)
            entry(key: "apiKey", value: apiKey)
        }
    }
}

ext {
    smackVersion = '4.2.1-SNAPSHOT'
    supportVersion = '25.3.1'
}

dependencies {
    compile project('MemorizingTrustManager')
    compile project('emojicon')

    compile 'com.github.str4tocaster.smack:smack-android-extensions:ffb99cc'
    compile 'com.github.str4tocaster.smack:smack-experimental:ffb99cc'
    compile 'com.github.str4tocaster.smack:smack-tcp:ffb99cc'
    compile 'com.github.str4tocaster.smack:smack-resolver-dnsjava:ffb99cc'

    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:support-v13:$supportVersion"

    compile 'com.github.Str4tocaster:otr4j:custom-SNAPSHOT'
    compile 'com.google.zxing:android-integration:3.3.0'

    // retrofit
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'

    // rx java
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'

    //social
    compile 'com.facebook.android:facebook-android-sdk:4.23.0'
    compile 'com.twitter.sdk.android:twitter:3.0.0'
    compile 'com.google.android.gms:play-services-auth:9.0.0'
    compile ('com.google.api-client:google-api-client-android:1.22.0') {
        exclude module: 'httpclient'
    }

    compile 'me.relex:circleindicator:1.2.2@aar'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'org.greenrobot:eventbus:3.0.0'

    compile 'com.soundcloud.android:android-crop:1.0.1@aar'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
}

configurations {
    all*.exclude group: 'xpp3', module: 'xpp3'
}
